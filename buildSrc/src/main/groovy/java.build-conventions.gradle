plugins {
    id 'com.adarshr.test-logger'
    id 'idea'
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }

        intTest(JvmTestSuite) {
            sources.java.srcDirs = ['src/int-test/java']
            sources.resources.srcDirs = ['src/int-test/resources']

            dependencies {
                implementation project()
                implementation sourceSets.test.output
            }

            targets {
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }
        }
    }
}

configurations {
    intTestImplementation.extendsFrom testImplementation
}

tasks.named('check') {
    dependsOn(testing.suites.intTest)
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
}

project.ext {
    imageName = rootProject.name + '/' + project.name
    imageTag = 'latest'
}

idea {
    module {
        downloadJavadoc = true
    }
}
